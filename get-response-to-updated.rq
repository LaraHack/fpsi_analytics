prefix foaf: <http://xmlns.com/foaf/0.1/>
prefix dct: <http://purl.org/dc/terms/>
prefix np: <http://www.nanopub.org/nschema#>
prefix npx: <http://purl.org/nanopub/x/>
prefix npa: <http://purl.org/nanopub/admin/>
prefix frbr: <http://purl.org/vocab/frbr/core#>
prefix lf: <https://w3id.org/linkflows/reviews/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix pso: <http://purl.org/spar/pso/>

select distinct ?resp_np ?update_np where {
  graph npa:graph { ?submission_np npa:hasValidSignatureForPublicKey ?pubkey . }
    ?submission_np np:hasAssertion ?submission_np_a ; np:hasPublicationInfo ?submission_np_i .
  graph ?submission_np_a {
    ?submitted_np frbr:partOf <https://w3id.org/linkflows/formalization-papers/DataScienceSpecialIssue> ;
      pso:withStatus pso:submitted .
  }
  graph ?submission_np_i {
    ?submission_np dct:creator ?__author_iri .
  }
  optional {
    graph npa:graph { ?newversion_submission_np npa:hasValidSignatureForPublicKey ?pubkey . } ?newversion__submission_np np:hasPublicationInfo ?si .
    graph ?si { ?newversion_submission_np npx:supersedes ?submission_np . }
  }
  filter not exists {
    graph npa:graph { ?retraction_submission_np npa:hasValidSignatureForPublicKey ?pubkey . } ?retraction_submission_np np:hasAssertion ?sra .
    graph ?sra { ?somebody_submission_np npx:retracts ?submission_np . }
  }
  optional {
    graph npa:graph { ?newversion_submitted_np npa:hasValidSignatureForPublicKey ?pubkey . } ?newversion_submitted_np np:hasPublicationInfo ?di .
    graph ?di { ?newversion_submitted_np npx:supersedes ?submitted_np . }
  }
  filter not exists {
    graph npa:graph { ?retraction_submitted_np npa:hasValidSignatureForPublicKey ?pubkey . } ?retraction_submitted_np np:hasAssertion ?dra .
    graph ?dra { ?somebody_submitted_np npx:retracts ?submitted_np . }
  }
  graph ?review_np_head {
    ?review_np np:hasAssertion ?review_np_assertion ;
      np:hasPublicationInfo ?review_np_pubinfo ;
      a np:Nanopublication .
  }
  graph ?review_np_assertion {
    ?review_comment a lf:ReviewComment .
    ?review_comment lf:refersTo ?submitted_np .
  }
  graph ?review_np_pubinfo {
    ?review_np dct:creator ?__reviewer_iri .
    bind(?__reviewer_iri as ?reviewer)
  }
  graph npa:graph {
    ?review_np npa:hasValidSignatureForPublicKey ?reviewer_pubkey .
  }
  optional {
    graph npa:graph { ?newversion_review_np npa:hasValidSignatureForPublicKey ?reviewer_pubkey . } ?newversion_review_np np:hasPublicationInfo ?ri .
    graph ?ri { ?newversion_review_np npx:supersedes ?review_np . }
  }
  filter not exists {
    graph npa:graph { ?retraction_review_np npa:hasValidSignatureForPublicKey ?reviewer_pubkey . } ?retraction_review_np np:hasAssertion ?ra .
    graph ?ra { ?somebody_review_np npx:retracts ?review_np . }
  }
  graph npa:graph { ?resp_np npa:hasValidSignatureForPublicKey ?pubkey . } ?resp_np np:hasAssertion ?respa .
  graph ?respa {
    ?resp_comment lf:isResponseTo ?review_np .
    ?resp_comment lf:refersTo ?update_np .
  }
  optional {
    graph npa:graph { ?newversion_resp_np npa:hasValidSignatureForPublicKey ?pubkey . } ?newversion_resp_np np:hasPublicationInfo ?respri .
    graph ?respri { ?newversion_resp_np npx:supersedes ?resp_np . }
  }
  filter not exists {
    graph npa:graph { ?retraction_resp_np npa:hasValidSignatureForPublicKey ?pubkey . } ?retraction_resp_np np:hasAssertion ?respra .
    graph ?respra { ?somebody_resp_np npx:retracts ?resp_np . }
  }
}
