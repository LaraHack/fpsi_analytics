prefix foaf: <http://xmlns.com/foaf/0.1/>
prefix dct: <http://purl.org/dc/terms/>
prefix np: <http://www.nanopub.org/nschema#>
prefix npx: <http://purl.org/nanopub/x/>
prefix npa: <http://purl.org/nanopub/admin/>
prefix frbr: <http://purl.org/vocab/frbr/core#>
prefix lf: <https://w3id.org/linkflows/reviews/>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix pso: <http://purl.org/spar/pso/>

prefix Addressed: <https://w3id.org/linkflows/reviews/PointAddressedComment>
prefix PartiallyAddressed: <https://w3id.org/linkflows/reviews/PointPartiallyAddressedComment>
prefix NotAddressed: <https://w3id.org/linkflows/reviews/PointNotAddressedComment>

prefix Agreed: <https://w3id.org/linkflows/reviews/AgreementComment>
prefix PartialAgreed: <https://w3id.org/linkflows/reviews/PartialAgreementComment>
prefix Disagreed: <https://w3id.org/linkflows/reviews/DisagreementComment>

select distinct ?resp_np ?update_np where {
  graph npa:graph { ?submission_np npa:hasValidSignatureForPublicKey ?pubkey . }
    ?submission_np np:hasAssertion ?submission_np_a ; np:hasPublicationInfo ?submission_np_i .  # TODO: move into npa:graph
  graph ?submission_np_a {
    ?submitted_np frbr:partOf <https://w3id.org/linkflows/formalization-papers/DataScienceSpecialIssue> ;
      pso:withStatus pso:submitted .
  }
  graph ?submission_np_i {
    ?submission_np dct:creator ?__author_iri .
  }
  filter not exists {
    graph npa:graph { ?retraction npa:hasValidSignatureForPublicKey ?pubkey . } ?retraction np:hasAssertion ?sra .  # TODO: move into npa:graph
    graph ?sra { ?somebody npx:retracts ?submission_np . }
  }
  optional {
    graph npa:graph { ?newversion_submission_np npa:hasValidSignatureForPublicKey ?pubkey . } ?newversion__submission_np np:hasPublicationInfo ?si .  # TODO: move into npa:graph
    graph ?si { ?newversion_submission_np npx:supersedes ?submission_np . }
  }
  filter not exists {
    graph npa:graph { ?retraction npa:hasValidSignatureForPublicKey ?pubkey . } ?retraction np:hasAssertion ?dra .  # TODO: move into npa:graph
    graph ?dra { ?somebody npx:retracts ?submitted_np . }
  }
  optional {
    graph npa:graph { ?newversion_submitted_np npa:hasValidSignatureForPublicKey ?pubkey . } ?newversion_submitted_np np:hasAssertion ?dsa .  # TODO: move into npa:graph
    graph ?dsa { ?newversion_submitted_np npx:supersedes ?submission_np . }
  }
  graph npa:graph { ?update_np npa:hasValidSignatureForPublicKey ?pubkey2 . } ?update_np np:hasPublicationInfo ?ui .  # TODO: move into npa:graph;
  graph ?ui { ?update_np lf:isUpdateOf ?submitted_np . }
  optional {
    graph npa:graph { ?newversion_update_np npa:hasValidSignatureForPublicKey ?pubkey2 . } ?newversion_update_np np:hasPublicationInfo ?usi .  # TODO: move into npa:graph
    graph ?usi { ?newversion_update_np npx:supersedes ?update_np . }
  }
  graph ?review_np_head {
    ?review_np np:hasAssertion ?review_np_assertion ;
      np:hasPublicationInfo ?review_np_pubinfo ;
      a np:Nanopublication .
  }
  graph ?review_np_assertion {
    ?review_comment a lf:ReviewComment .
    ?review_comment lf:refersTo ?submitted_np .
  }
  graph ?review_np_pubinfo {
    ?review_np dct:creator ?__reviewer_iri .
    bind(?__reviewer_iri as ?reviewer)
  }
  optional { ?__reviewer_iri foaf:name ?reviewerLabel . }
  graph npa:graph {
    ?review_np npa:hasValidSignatureForPublicKey ?reviewer_pubkey .
  }
  optional {
    graph npa:graph { ?newversion_review npa:hasValidSignatureForPublicKey ?reviewer_pubkey . } ?newversion_review np:hasPublicationInfo ?ri .
    graph ?ri { ?newversion_review npx:supersedes ?review_np . }
  }
  optional {
    graph npa:graph { ?retraction npa:hasValidSignatureForPublicKey ?reviewer_pubkey . } ?retraction np:hasAssertion ?ra .  # TODO: move into npa:graph
    graph ?ra { ?somebody npx:retracts ?review_np . }
  }
  graph npa:graph { ?resp_np npa:hasValidSignatureForPublicKey ?pubkey . } ?resp_np np:hasAssertion ?respa .
  graph ?respa { ?resp_np lf:isResponseTo ?review_np .}
  filter not exists {
    graph npa:graph { ?retraction npa:hasValidSignatureForPublicKey ?pubkey . } ?retraction np:hasAssertion ?respra .  # TODO: move into npa:graph
    graph ?respra { ?somebody npx:retracts ?resp_np . }
  }
  optional {
    graph npa:graph { ?newversion_response_np npa:hasValidSignatureForPublicKey ?pubkey . } ?newversion_response_np np:hasAssertion ?respda .  # TODO: move into npa:graph
    graph ?respda { ?newversion_response_np npx:supersedes ?resp_np . }
  }
}
