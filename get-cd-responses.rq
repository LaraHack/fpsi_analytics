prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>
prefix foaf: <http://xmlns.com/foaf/0.1/>
prefix dct: <http://purl.org/dc/terms/>
prefix np: <http://www.nanopub.org/nschema#>
prefix npx: <http://purl.org/nanopub/x/>
prefix npa: <http://purl.org/nanopub/admin/>
prefix vp: <http://purl.org/nanopub/admin/hasValidSignatureForPublicKey>
prefix frbr: <http://purl.org/vocab/frbr/core#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix pso: <http://purl.org/spar/pso/>

prefix Addressed: <https://w3id.org/linkflows/reviews/PointAddressedComment>
prefix PartiallyAddressed: <https://w3id.org/linkflows/reviews/PointPartiallyAddressedComment>
prefix NotAddressed: <https://w3id.org/linkflows/reviews/PointNotAddressedComment>

prefix Agreed: <https://w3id.org/linkflows/reviews/AgreementComment>
prefix PartialAgreed: <https://w3id.org/linkflows/reviews/PartialAgreementComment>
prefix Disagreed: <https://w3id.org/linkflows/reviews/DisagreementComment>

prefix lf: <https://w3id.org/linkflows/reviews/>

select distinct ?resp_np ?newversion_response where {
  graph ?submission_np_head {
    ?submission_np np:hasAssertion ?submission_np_assertion ;
      np:hasPublicationInfo ?submission_np_pubinfo .
  }
  graph ?submission_np_assertion {
    ?submitted_np frbr:partOf <https://w3id.org/linkflows/formalization-papers/DataScienceSpecialIssue> ;
      pso:withStatus pso:submitted .
  }
  graph ?submission_np_pubinfo {
    ?submission_np dct:creator ?__author_iri .
    bind(?__author_iri as ?author)
  }
  optional { ?__author_iri foaf:name ?authorLabel . }
  graph npa:graph {
    ?submission_np vp: ?pubkey .
  }
  optional {
    graph npa:graph { ?retraction vp: ?pubkey . } ?retraction np:hasAssertion ?sa .  # TODO: move into npa:graph
    graph ?sa { ?somebody npx:retracts ?submission_np . }
  }
  graph ?submitted_np_head {
    ?submitted_np np:hasAssertion ?submitted_np_assertion .
  }
  graph ?submitted_np_assertion {
    ?submitted_np_s ?submitted_np_p ?class .
  }
  graph ?sp_np_head {
    ?sp_np np:hasAssertion ?sp_np_assertion .
  }
  graph ?sp_np_assertion {
    ?spi ?p ?class .
  }
  graph npa:graph {
    ?sp_np vp: ?pubkey .
  }
  graph ?class_np_head {
    ?class_np np:hasAssertion ?class_np_assertion ;
      np:hasPublicationInfo ?class_np_pubinfo .
  }
  graph ?class_np_assertion {
    ?class rdfs:label ?class_name .
  }
  graph ?class_np_pubinfo {
    ?class_np npx:introduces ?class .
  }
  graph npa:graph {
    ?class_np vp: ?pubkey .
  }
  graph ?review_np_head {
    ?review_np np:hasAssertion ?review_np_assertion ;
      np:hasPublicationInfo ?review_np_pubinfo ;
      a np:Nanopublication .
  }
  graph ?review_np_assertion {
    ?r a lf:ReviewComment .
    ?r lf:refersTo ?class_np .
    optional {
      ?r lf:refersToMentioningOf ?mentioning .
    }
    ?r a ?aspect .
    values ?aspect { lf:ContentComment lf:StyleComment lf:SyntaxComment }
    ?r a ?pos .
    values ?pos { lf:PositiveComment lf:NeutralComment lf:NegativeComment }
    ?r a ?action .
    values ?action { lf:ActionNeededComment lf:NoActionNeededComment lf:SuggestionComment }
    ?r lf:hasImpact ?imp .
    ?r lf:hasCommentText ?text .
  }
  graph ?review_np_pubinfo {
    ?review_np dct:creator ?__reviewer_iri .
    bind(?__reviewer_iri as ?reviewer)
  }
  optional { ?__reviewer_iri foaf:name ?reviewerLabel . }
  graph npa:graph {
    ?review_np vp: ?reviewer_pubkey .
  }
  optional {
    graph npa:graph { ?newversion_review vp: ?reviewer_pubkey . } ?newversion_review np:hasPublicationInfo ?ri .  # TODO: move into npa:graph
    graph ?ri { ?newversion_review npx:supersedes ?review_np . }
  }
  optional {
    graph npa:graph { ?retraction vp: ?reviewer_pubkey . } ?retraction np:hasAssertion ?ra .  # TODO: move into npa:graph
    graph ?ra { ?somebody npx:retracts ?review_np . }
  }
  optional {
    graph npa:graph { ?update_np vp: ?pubkey . } ?update_np np:hasPublicationInfo ?xi .  # TODO: move into npa:graph
    graph ?xi { ?update_np lf:isUpdateOf ?class_np . }
  }
  graph npa:graph { ?resp_np vp: ?pubkey . } ?resp_np np:hasAssertion ?respa .
  graph ?respa {
    ?resp_comment lf:isResponseTo ?review_np ;
      lf:hasCommentText ?resp_textpre .
    bind(strdt(replace(?resp_textpre, "[^\n\t\r -Ùèøø]", "[NON-PRINTABLE-CHARACTER]"), xsd:string) as ?resp_text)
    ?resp_comment a ?agreement .
    values ?agreement { lf:AgreementComment lf:DisagreementComment lf:PartialAgreementComment }
    ?resp_comment a ?addressed .
    values ?addressed { lf:PointAddressedComment lf:PointNotAddressedComment lf:PointPartiallyAddressedComment }
  }
  optional {
    graph npa:graph { ?retraction vp: ?pubkey . } ?retraction np:hasAssertion ?respra .  # TODO: move into npa:graph
    graph ?respra { ?somebody npx:retracts ?resp_np . }
  }
  optional {
    graph npa:graph { ?newversion_response vp: ?pubkey . } ?newversion_response np:hasAssertion ?respra .  # TODO: move into npa:graph
    graph ?respra { ?newversion_response npx:supersedes ?resp_np . }
  }
}
