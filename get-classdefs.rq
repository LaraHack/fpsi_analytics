prefix foaf: <http://xmlns.com/foaf/0.1/>
prefix dct: <http://purl.org/dc/terms/>
prefix np: <http://www.nanopub.org/nschema#>
prefix npx: <http://purl.org/nanopub/x/>
prefix npa: <http://purl.org/nanopub/admin/>
prefix vp: <http://purl.org/nanopub/admin/hasValidSignatureForPublicKey>
prefix frbr: <http://purl.org/vocab/frbr/core#>
prefix xsd: <http://www.w3.org/2001/XMLSchema#>
prefix pso: <http://purl.org/spar/pso/>

prefix lf: <https://w3id.org/linkflows/reviews/>

select distinct ?class_np ?newversion_class_np where {
  graph npa:graph { ?submission_np npa:hasValidSignatureForPublicKey ?pubkey . }
    ?submission_np np:hasAssertion ?submission_np_a ; np:hasPublicationInfo ?submission_np_i .
  graph ?submission_np_a {
    ?submitted_np frbr:partOf <https://w3id.org/linkflows/formalization-papers/DataScienceSpecialIssue> ;
      pso:withStatus pso:submitted .
  }
  graph ?submission_np_i {
    ?submission_np dct:creator ?__author_iri .
    bind(?__author_iri as ?author)
  }
  optional {
    graph npa:graph { ?newversion_submission_np npa:hasValidSignatureForPublicKey ?pubkey . } ?newversion_submission_np np:hasPublicationInfo ?si .
    graph ?si { ?newversion_submission_np npx:supersedes ?submission_np . }
  }
  filter not exists {
    graph npa:graph { ?retraction_submission_np npa:hasValidSignatureForPublicKey ?pubkey . } ?retraction_submission_np np:hasAssertion ?sra .
    graph ?sra { ?somebody_submission_np npx:retracts ?submission_np . }
  }
  optional {
    graph npa:graph { ?newversion_submitted_np npa:hasValidSignatureForPublicKey ?pubkey . } ?newversion_submitted_np np:hasPublicationInfo ?di .
    graph ?di { ?newversion_submitted_np npx:supersedes ?submitted_np . }
  }
  filter not exists {
    graph npa:graph { ?retraction_submitted_np npa:hasValidSignatureForPublicKey ?pubkey . } ?retraction_submitted_np np:hasAssertion ?dra .
    graph ?dra { ?somebody_submitted_np npx:retracts ?submitted_np . }
  }
  ?submitted_np np:hasAssertion ?submitted_np_a .
  graph ?submitted_np_a {
    ?spi ?spi_pred ?class .
  }
  graph npa:graph { ?class_np npa:hasValidSignatureForPublicKey ?pubkey . }
  ?class_np np:hasAssertion ?class_np_a ; np:hasPublicationInfo ?class_np_i .
  graph ?class_np_i { ?class_np npx:introduces ?class . }
  optional {
    graph npa:graph { ?newversion_class_np vp: ?pubkey . } ?newversion_class np:hasPublicationInfo ?ri .
    graph ?ri { ?newversion_class_np npx:supersedes ?class_np . }
  }
  filter not exists {
    graph npa:graph { ?retraction_class_np vp: ?pubkey . } ?retraction_class_np np:hasAssertion ?ra .
    graph ?ra { ?somebody_class_np npx:retracts ?class_np . }
  }
}
